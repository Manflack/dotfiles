#!/usr/bin/env zsh

LAMBDA_THEME_MINIMAL=${LAMBDA_THEME_MINIMAL:-false}
LAMBDA_THEME_MODE=${LAMBDA_THEME_MODE:-"dark"}
LAMBDA_THEME_PROMPT_IN_NEW_LINE=${LAMBDA_THEME_PROMPT_IN_NEW_LINE:-false}

 [[ $(echotc Co) -gt 100 ]] && support_color_tones=true || support_color_tones=false

status_icon_ok="λ"
status_icon_ko="Λ"
git_no_changes_status="✓"
git_dirty_status="✗"

if [ "$LAMBDA_THEME_MODE" = "dark" ]; then
  git_branch_color="green"
  git_dirty_status_color="yellow"
  git_no_changes_status_color="white"
  git_on_branch_color="white"
  pwd_color="yellow"
  status_icon_color="white"
  [[ $support_color_tones = true ]] && ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=8' || ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=magenta'
else
  git_branch_color="black"
  git_dirty_status_color="magenta"
  git_no_changes_status_color="green"
  git_on_branch_color="green"
  pwd_color="black"
  status_icon_color="black"
  [[ $support_color_tones = true ]] && ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=8' || ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=magenta'
fi

prompt_lambda_pwd() {
  local -r prompt_short_dir=$("$DOTFILES_PATH/bin/dot" core short_pwd)
  print -n "%F{$pwd_color}${prompt_short_dir}"
}

prompt_lambda_git() {
  [[ -n ${git_info} ]] && print -n "%F{$git_on_branch_color} on${(e)git_info[prompt]}"
}

prompt_lambda_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_lambda_setup() {
  local prompt_lambda_status="%(?:%F{green}{%F{$status_icon_color}$status_icon_ok%F{green}}:%F{red}{%F{$status_icon_color}$status_icon_ko%F{red}})"

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_lambda_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format "%F{$git_branch_color}%b"
  zstyle ':zim:git-info:commit' format "%c"
  zstyle ':zim:git-info:clean' format "%F{$git_no_changes_status_color}$git_no_changes_status"
  zstyle ':zim:git-info:dirty' format "%F{$git_dirty_status_color}$git_dirty_status"
  zstyle ':zim:git-info:keys' format "prompt" " %F{cyan}%b%c %C%D"

  if [ "$LAMBDA_THEME_MINIMAL" = true ]; then
    PS1="${prompt_lambda_status} "
  else
    PS1="${prompt_lambda_status} \$(prompt_lambda_pwd)\$(prompt_lambda_git)%f "
  fi

  if [ "$LAMBDA_THEME_PROMPT_IN_NEW_LINE" = true ]; then
    PS1="$PS1"$'\n'
  fi

  RPS1=''
}

prompt_lambda_setup "${@}"
